------------------------------------------------------------------------------
--                                                                          --
--               Generated by RecordFlux 0.25.0 on 2025-06-28               --
--                                                                          --
--                     Copyright (C) 2018-2025 AdaCore                      --
--                                                                          --
--         SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception          --
--                                                                          --
------------------------------------------------------------------------------

with CoAP_SPARK.Messages.Encoding;
with CoAP_SPARK.Log;

with RFLX.CoAP;
with RFLX.CoAP.CoAP_Message;
with RFLX.RFLX_Types;

package body RFLX.CoAP_Server.Main_Loop
  with SPARK_Mode
is

   procedure Get_Response
     (State       : in out RFLX.CoAP_Server.Main_Loop_Environment.State;
      Request     : RFLX.CoAP_Server.Definite_Message.Structure;
      RFLX_Result : out RFLX.CoAP_Server.Application_Response.Structure)
   is
      use type CoAP_SPARK.Status_Type;
      use type RFLX.CoAP.Code_Class;

      Buffer  : RFLX_Types.Bytes_Ptr :=
        new RFLX_Types.Bytes'(Request.Message_Data (Request.Message_Data'First ..
                                                      RFLX_Types.Index'Base (Request.Length)));
      Context : RFLX.CoAP.CoAP_Message.Context;

      Request_Content  : CoAP_SPARK.Messages.Content;
      Response_Codes   : CoAP_SPARK.Messages.Response_Kind;
      Response_Content : CoAP_SPARK.Messages.Content;

      Last : constant RFLX_Types.Bit_Length :=
           RFLX_Types.To_Last_Bit_Index (RFLX_Types.Length (Request.Length));

   begin

      RFLX_Result :=
         (Message_Class        => RFLX.CoAP.Server_Error,
          Success_Code         => RFLX.CoAP.Success_Response'Last,
          Client_Error_Code    => RFLX.CoAP.Client_Error_Response'Last,
          Server_Error_Code    => RFLX.CoAP.Internal_Server_Error,
          Padding              => 0,
          Options_And_Payload_Options_And_Payload => [others => 0],
          Options_And_Payload_Length => 0);

      if Buffer'Length = 0 then
         -- Not really expected, because the message has already been validated
         -- in the main loop state machine.
         State.Current_Status := CoAP_SPARK.Unexpected_Case;
         RFLX.RFLX_Types.Free (Buffer);
         return;
      end if;

      RFLX.CoAP.CoAP_Message.Initialize
        (Ctx          => Context,
         Buffer       => Buffer,
         First        =>
           RFLX_Types.To_First_Bit_Index
             (RFLX_Types.Index (Request.Message_Data'First)),
         Last         => Last,
         Written_Last => Last);

      RFLX.CoAP.CoAP_Message.Verify_Message (Context);

      if not RFLX.CoAP.CoAP_Message.Well_Formed_Message (Context) then
         State.Current_Status := CoAP_SPARK.Malformed_Message;

      elsif RFLX.CoAP.CoAP_Message.Get_Class (Context) /= RFLX.CoAP.Request then
         State.Current_Status := CoAP_SPARK.Invalid_Request;

      else
         Handle_Request :
         declare
            use type RFLX.RFLX_Types.Length;
            Opt_Payload_Length : constant RFLX_Types.Length :=
              RFLX_Types.To_Length
                (RFLX.CoAP.CoAP_Message.Field_Size
                   (Context, RFLX.CoAP.CoAP_Message.F_Options_And_Payload));
            Opt_Payload_Buffer : RFLX_Types.Bytes
                (1 .. RFLX.RFLX_Types.Index'Base (Opt_Payload_Length));
            Encoded_Length : RFLX.RFLX_Types.Length;
         begin
            RFLX.CoAP.CoAP_Message.Get_Options_And_Payload
              (Context, Opt_Payload_Buffer);

            CoAP_SPARK.Messages.Encoding.Decode_Options_And_Payload
              (Data            => Opt_Payload_Buffer,
               Status          => State.Current_Status,
               Decoded_Content => Request_Content);

            if State.Current_Status = CoAP_SPARK.OK
              and then State.Request_Handler not in null
            then

               CoAP_SPARK.Log.Put_Line ("REQUEST: ");
               CoAP_SPARK.Messages.Print_Content
                 (Item              => Request_Content,
                  Log_Level_Payload => CoAP_SPARK.Log.Debug);

               -- Call the request handler with the decoded content
               State.Request_Handler
                 (Method           =>
                    RFLX.CoAP.CoAP_Message.Get_Method (Context),
                  Request_Content  => Request_Content,
                  Response_Codes   => Response_Codes,
                  Response_Content => Response_Content);

               CoAP_SPARK.Log.Put_Line ("RESPONSE: ");
               CoAP_SPARK.Messages.Print_Content
                 (Item              => Response_Content,
                  Log_Level_Payload => CoAP_SPARK.Log.Debug);
               CoAP_SPARK.Messages.Print_Response_Kind
                 (Item => Response_Codes);

               CoAP_SPARK.Messages.Encoding.Encode_Options_And_Payload
                 (Options_And_Payload => Response_Content,
                  Status              => State.Current_Status,
                  Encoded_Data        =>
                    RFLX_Result.Options_And_Payload_Options_And_Payload,
                  Encoded_Length      => Encoded_Length);

               if Encoded_Length > RFLX.RFLX_Types.Length (RFLX.CoAP.Length_16'Last) then
                  State.Current_Status := CoAP_SPARK.Capacity_Error;
                  RFLX_Result.Options_And_Payload_Length := 0;
               else
                  RFLX_Result.Options_And_Payload_Length :=
                    RFLX.CoAP.Length_16 (Encoded_Length);
               end if;

               RFLX_Result.Success_Code := RFLX.CoAP.Success_Response'Last;
               RFLX_Result.Client_Error_Code :=
                 RFLX.CoAP.Client_Error_Response'Last;
               RFLX_Result.Server_Error_Code :=
                 RFLX.CoAP.Server_Error_Response'Last;
               RFLX_Result.Message_Class := Response_Codes.Code_Class;

               if State.Current_Status /= CoAP_SPARK.OK then

                  CoAP_SPARK.Log.Put
                    ("Error in options and payload encoding: ",
                     CoAP_SPARK.Log.Info);
                  CoAP_SPARK.Log.Put_Line
                    (State.Current_Status'Image, CoAP_SPARK.Log.Info);

                  RFLX_Result.Message_Class := RFLX.CoAP.Server_Error;
                  RFLX_Result.Server_Error_Code :=
                    RFLX.CoAP.Internal_Server_Error;
               else
                  case CoAP_SPARK.Messages.Response_Code
                         (Response_Codes.Code_Class)
                  is
                     when RFLX.CoAP.Success =>

                        RFLX_Result.Success_Code :=
                          Response_Codes.Success_Code;

                     when RFLX.CoAP.Client_Error =>

                        RFLX_Result.Client_Error_Code :=
                          Response_Codes.Client_Error_Code;

                     when RFLX.CoAP.Server_Error =>

                        RFLX_Result.Server_Error_Code :=
                          Response_Codes.Server_Error_Code;

                  end case;
               end if;
            end if;
         end Handle_Request;
      end if;

      RFLX.CoAP.CoAP_Message.Take_Buffer
        (Ctx    => Context,
         Buffer => Buffer);
      pragma Assert (not RFLX.CoAP.CoAP_Message.Has_Buffer (Context));

      RFLX.RFLX_Types.Free (Buffer);

      CoAP_SPARK.Messages.Finalize (Request_Content);
      pragma Assert (CoAP_SPARK.Messages.Is_Empty (Request_Content));
      CoAP_SPARK.Messages.Finalize (Response_Content);
      pragma Assert (CoAP_SPARK.Messages.Is_Empty (Response_Content));

   end Get_Response;

   procedure Get_Error_Options_And_Payload
     (State       : in out RFLX.CoAP_Server.Main_Loop_Environment.State;
      RFLX_Result : out RFLX.CoAP_Server.Options_And_Payload_Data.Structure)
   is
      use type CoAP_SPARK.Status_Type;
      use type RFLX.RFLX_Types.Length;

      Status_Image : constant String :=
        CoAP_SPARK.Status_Type'Image (State.Current_Status);
      Buffer  : RFLX_Types.Bytes_Ptr :=
        new RFLX_Types.Bytes'([1 .. RFLX_Result.Options_And_Payload'Length => 0]);
      Context : RFLX.CoAP.CoAP_Message.Context;

      Response_Content : CoAP_SPARK.Messages.Content;
      Encoded_Length : RFLX.RFLX_Types.Length;
   begin
      RFLX.CoAP.CoAP_Message.Initialize
        (Ctx    => Context,
         Buffer => Buffer);

      -- This procedure is only called when there is an error in the
      -- main loop state machine, so if we haven't set an error status yet,
      -- we have to set it to Unexpected_Case, since the cause is unknown.
      if State.Current_Status = CoAP_SPARK.OK or else
         Status_Image'Length > CoAP_SPARK.Max_Payload_Length
      then
         CoAP_SPARK.Messages.Initialize_With_Text_Payload
           (Text => "Unexpected case in main loop",
            Item => Response_Content);
      else
         CoAP_SPARK.Messages.Initialize_With_Text_Payload
           (Text => Status_Image,
            Item => Response_Content);
      end if;

      CoAP_SPARK.Messages.Encoding.Encode_Options_And_Payload
        (Options_And_Payload => Response_Content,
         Status              => State.Current_Status,
         Encoded_Data        => RFLX_Result.Options_And_Payload,
         Encoded_Length      => Encoded_Length);

      if Encoded_Length > RFLX.RFLX_Types.Length (RFLX.CoAP.Length_16'Last) then
         State.Current_Status := CoAP_SPARK.Capacity_Error;
         RFLX_Result.Length := 0;
      else
         RFLX_Result.Length :=
            RFLX.CoAP.Length_16 (Encoded_Length);
      end if;

      CoAP_SPARK.Messages.Finalize (Response_Content);
      pragma Assert (CoAP_SPARK.Messages.Is_Empty (Response_Content));

      RFLX.CoAP.CoAP_Message.Take_Buffer
        (Ctx    => Context,
         Buffer => Buffer);
      pragma Assert (not RFLX.CoAP.CoAP_Message.Has_Buffer (Context));

      RFLX.RFLX_Types.Free (Buffer);
   end Get_Error_Options_And_Payload;

end RFLX.CoAP_Server.Main_Loop;
